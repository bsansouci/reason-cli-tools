
let mime_for_name = ext => switch (String.lowercase(ext)) {
| "txt" => "text/plain"
| "html" => "text/html"
| "json" => "text/json"
| "js" => "application/javascript"
| "jpg" | "jpeg" => "image/jpeg"
| "png" => "image/png"
| "pdf" => "image/pdf"
| "ico" => "image/ico"
| "gif" => "image/gif"
| _ => "application/binary"
};

let ext = path => {
  let parts = Str.split(Str.regexp("\\."), path);
  List.nth(parts, List.length(parts) - 1)
};

let unwrap = (message, opt) => switch opt { | Some(x) => x | None => failwith(message)};

open BasicServer.Response;

let sendFile = (path, full_path) => switch (Files.readFile(full_path)) {
| Some(text) => Ok(mime_for_name(ext(path)), text)
| None => Bad(404, "File not found: " ++ path)
};

let serveStatic = (full_path, path) => {
  switch (Unix.stat(full_path)) {
  | exception Unix.Unix_error(Unix.ENOENT, _, _) => Bad(404, "File not found: " ++ path)
  | stat =>
  switch (stat.Unix.st_kind) {
  | Unix.S_REG => sendFile(path, full_path)
  | Unix.S_DIR => {
    let index = Filename.concat(full_path, "index.html");
    if (Files.isFile(index)) {
      sendFile(path, index)
    } else {
      Ok("text/plain", "Directory")
    }
  }
  | _ => Bad(404, "Unexpected file type: " ++ path)
  };
  }
};

let handler = (base, method, path, headers) => {
  switch (method) {
  | "GET" => {
    let full_path = Filename.concat(base, "." ++ path);
    serveStatic(full_path, path)
  }
  | _ => Bad(401, "Method not allowed: " ++ method)
  }
};

let run = (~poll=?, ~port, path) => BasicServer.listen(~poll=?poll, port, handler(path));